plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.12'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'jacoco'
}

group = 'com.hyundai-autoever'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '11'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	//스프링 기본 설정
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
	implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.4'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// queryDSL 설정
	implementation "com.querydsl:querydsl-jpa"
	implementation "com.querydsl:querydsl-core"
	implementation "com.querydsl:querydsl-collections"
	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa" // querydsl JPAAnnotationProcessor 사용 지정
	annotationProcessor "jakarta.annotation:jakarta.annotation-api" // java.lang.NoClassDefFoundError (javax.annotation.Generated) 대응 코드
	annotationProcessor "jakarta.persistence:jakarta.persistence-api" // java.lang.NoClassDefFoundError (javax.annotation.Entity) 대응 코드

	//스웨거 설정
	implementation group: 'io.springfox', name: 'springfox-boot-starter', version: '3.0.0'

	//Apache Tika 설정
	implementation group: 'org.apache.tika', name: 'tika-core', version: '2.8.0'
	implementation group: 'org.apache.tika', name: 'tika-langdetect', version: '1.28.5'

	//Jackson 설정
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core'

	//WebClient 설정
	implementation 'org.springframework.boot:spring-boot-starter-webflux:2.7.13'

	//S3 연결 설정
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

	//JWT 설정
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	//Lombok 설정
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	//MapStruct 매퍼 설정
	implementation 'org.mapstruct:mapstruct:1.5.3.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

	//테스트
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	//Junit 설정
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

	//Mockito 설정
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.3.1'

	//Jacoco 설정
	testImplementation group: 'org.jacoco', name: 'org.jacoco.agent', version: '0.8.8'
}

tasks.named('test') {
	useJUnitPlatform()
}

def generated = 'src/main/generated'

// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile) {
	options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// java source set 에 querydsl QClass 위치 추가
sourceSets {
	main.java.srcDirs += [ generated ]
}

// gradle clean 시에 QClass 디렉토리 삭제
clean {
	delete file(generated)
}

//Jacoco 설정
test {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

jacoco {
	toolVersion = '0.8.8'
}

jacocoTestReport {
	reports {
		html.enabled true
		xml.enabled false
		csv.enabled false
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"**/com/hyundaiautoever/HEAT/v1/config",
					"**/com/hyundaiautoever/HEAT/v1/dto",
					"**/com/hyundaiautoever/HEAT/v1/entity",
					"**/com/hyundaiautoever/HEAT/v1/exception",
					"**/com/hyundaiautoever/HEAT/v1/filter",
					"**/com/hyundaiautoever/HEAT/v1/util",
					"**/com/hyundaiautoever/HEAT/v1/repository",
					"**/com/hyundaiautoever/HEAT/v1/service/OpenAI*",
					"**/com/hyundaiautoever/HEAT/v1/service/Papago*"
			])
		}))
	}
	finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			enabled = true
			element = 'CLASS'
		}

	}
}
